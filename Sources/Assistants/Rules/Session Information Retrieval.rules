;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;   Modular Simulator Controller System - Session Information Retrieval   ;;;
;;;                                                                         ;;;
;;;   Author:     Oliver Juwig (TheBigO)                                    ;;;
;;;   License:    (2023) Creative Commons - BY-NC-SA                        ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;-------------------------------------------------------------------------;;;
;;;                            Rule Include Section                         ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Lap Information Retrieval.rules
#Include %kResourcesDirectory%Rules\Pitstop Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                            Public Rules Section                         ;;;
;;;-------------------------------------------------------------------------;;;

firstStintLap(?lap) <= pitstopLap(?lap), !
firstStintLap(1)

nextStintLaps(?laps) <= lapRemainingFuel(!Lap, ?remainingFuel), ?fuel = ?remainingFuel + !Fuel.Amount.Target, !,
						lapAvgConsumption(!Lap, ?fuelPerLap), ?laps = ?fuel / ?fuelPerLap
nextStintLaps(?laps) <= remainingSessionLaps(!Lap, ?laps)

postRaceLaps(1) <= !Session.Settings.Lap.PostRace = true
postRaceLaps(0)

remainingSessionLaps(?lap, ?remainingLaps) <= Get(Session.Format, Lap), !, Get(Session.Lap.Remaining, ?temp),
											  postRaceLaps(?postRaceLaps), ?remainingLaps = ?temp + ?postRaceLaps
remainingSessionLaps(?lap, ?remainingLaps) <= remainingSessionTime(?lap, ?remainingTime), lapAvgTime(?lap, ?avgTime),
											  postRaceLaps(?postRaceLaps), ?temp = ?remainingTime / ?avgTime,
											  ?remainingLaps = ?temp + ?postRaceLaps

remainingSessionTime(?lap, ?remainingTime) <= Get(Session.Format, Time), !, get(Session.Time.Remaining, ?remainingTime)
remainingSessionTime(?lap, ?remainingTime) <= Get(Lap, ?lap, Time.End, ?sessionTime), ?sessionLength = !Session.Duration * 1000,
											  ?remainingTime = ?sessionLength - ?sessionTime

remainingStintLaps(?lap, ?remainingLaps) <= remainingStintLaps(?lap, Fuel, ?fuelLaps), remainingStintLaps(?lap, Driver, ?driverLaps),
											min(?fuelLaps, ?driverLaps, ?remainingLaps), remainingSessionLaps(?lap, ?sessionLaps),
											?remainingLaps < ?sessionLaps, !
remainingStintLaps(?lap, ?remainingLaps) <= remainingSessionLaps(?lap, ?remainingLaps)

remainingStintLaps(?lap, Fuel, ?remainingLaps) <= remainingStintLaps(?lap, Fuel, ?remainingLaps, false)
remainingStintLaps(?lap, Driver, ?remainingLaps) <= remainingStintTime(?stintTime), lapAvgTime(?lap, ?avgTime),
													?remainingLaps = ?stintTime / ?avgTime

remainingStintLaps(?lap, Fuel, ?remainingLaps, true) <= lapRemainingFuel(?lap, ?temp), lapAvgConsumption(?lap, ?fuelPerLap),
														safetyFuel(?fuelPerLap, ?safetyFuel), ?fuelLeft = ?temp - ?safetyFuel,
														?remainingLaps = ?fuelLeft / ?fuelPerLap
remainingStintLaps(?lap, Fuel, ?remainingLaps, false) <= lapRemainingFuel(?lap, ?fuelLeft), lapAvgConsumption(?lap, ?fuelPerLap),
														 ?remainingLaps = ?fuelLeft / ?fuelPerLap

remainingStintTime(?lap, ?remainingTime) <= remainingStintLaps(?lap, Driver, ?remainingLaps), lapAvgTime(?lap, ?avgTime),
											?remainingTime = ?remainingLaps * ?avgTime, remainingSessionTime(?lap, ?sessionTime),
											?remainingTime < ?sessionTime, !
remainingStintTime(?lap, ?remainingTime) <= remainingSessionTime(?lap, ?remainingTime)
											
remainingStintTime(?stintTime) <= Get(Driver.Time.Stint.Remaining, ?stintTime)

safetyFuel(?avgConsumption, ?safetyFuel) <= !Session.Settings.Lap.PostRace = true, ?safetyFuel = ?avgConsumption + !Session.Settings.Fuel.SafetyMargin, !
safetyFuel(?, !Session.Settings.Fuel.SafetyMargin)