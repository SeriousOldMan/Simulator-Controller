;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;   Modular Simulator Controller System - Grip Notifications              ;;;
;;;                                                                         ;;;
;;;   Author:     Oliver Juwig (TheBigO)                                    ;;;
;;;   License:    (2025) Creative Commons - BY-NC-SA                        ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;-------------------------------------------------------------------------;;;
;;;                            Rule Include Section                         ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Tyre Information Retrieval.rules
#Include %kResourcesDirectory%Rules\Session Information Retrieval.rules
#Include %kResourcesDirectory%Rules\Weather Notifications.rules


;;;-------------------------------------------------------------------------;;;
;;;                            Public Rules Section                         ;;;
;;;-------------------------------------------------------------------------;;;

priority: 5, {Any: [?Track.Grip], {None: [?Track.Grip.Index]}} => (Prove: updateGripIndex())

updateGripIndex() <= gripIndex(!Track.Grip, ?index), Set(Track.Grip.Index, ?index)

priority: 1,
	{All: [?Tyre.Compound = Dry], {None: [?Weather.Tyre.Change.Recommended]},
		  [!Lap.Remaining.Session > ?Session.Settings.Pitstop.Service.Last],
		  [?Track.Grip.Index > 3], [?Weather.Weather.10Min.Index > 3],
		  {Prove: availableTyreCompound(Wet, ?)}} =>
		(Prove: gripTyreChangeRecommendation(!Lap, Wet))
priority: 1,
	{All: [?Tyre.Compound = Dry], {None: [?Weather.Tyre.Change.Recommended]},
		  [!Lap.Remaining.Session > ?Session.Settings.Pitstop.Service.Last],
		  [?Track.Grip.Index > 3], [?Weather.Weather.10Min.Index > 2],
		  {Prove: availableTyreCompound(Intermediate, ?)}} =>
		(Prove: gripTyreChangeRecommendation(!Lap, Intermediate))
priority: 1,
	{All: [?Tyre.Compound = Intermediate], {None: [?Weather.Tyre.Change.Recommended]},
		  [!Lap.Remaining.Session > ?Session.Settings.Pitstop.Service.Last],
		  [?Track.Grip.Index < 4], [?Weather.Weather.10Min = Dry],
		  {Prove: availableTyreCompound(Dry, ?)}} =>
		(Prove: gripTyreChangeRecommendation(!Lap, Dry))
priority: 1,
	{All: [?Tyre.Compound = Intermediate], {None: [?Weather.Tyre.Change.Recommended]},
		  [!Lap.Remaining.Session > ?Session.Settings.Pitstop.Service.Last],
		  [?Track.Grip.Index > 5], [?Weather.Weather.10Min.Index > 2],
		  {Prove: availableTyreCompound(Wet, ?)}} =>
		(Prove: gripTyreChangeRecommendation(!Lap, Wet))
priority: 1,
	{All: [?Tyre.Compound = Wet], {None: [?Weather.Tyre.Change.Recommended]},
		  [!Lap.Remaining.Session > ?Session.Settings.Pitstop.Service.Last],
		  [?Track.Grip.Index < 5], [?Weather.Weather.10Min = Dry],
		  {Prove: availableTyreCompound(Dry, ?)}} =>
		(Prove: gripTyreChangeRecommendation(!Lap, Dry))
priority: 1,
	{All: [?Tyre.Compound = Wet], {None: [?Weather.Tyre.Change.Recommended]},
		  [!Lap.Remaining.Session > ?Session.Settings.Pitstop.Service.Last],
		  [?Track.Grip.Index < 6], [?Weather.Weather.10Min.Index < 4],
		  {Prove: availableTyreCompound(Intermediate, ?)}} =>
		(Prove: gripTyreChangeRecommendation(!Lap, Intermediate))

{All: [?Lap > 1], [?Tyre.Compound = Dry],
	  {Any: {All: {None: [?Weather.Change.Notified]}, [?Weather.Weather.Now.Index <= 2], [?Weather.Weather.30Min.Index >= 3]},
			[?Weather.Change.Notified < ?Weather.Weather.30Min.Index]}} =>
		(Prove: weatherForecastNotification(?Lap, !Weather.Weather.30Min, 30))
{All: [?Lap > 1], [?Tyre.Compound = Intermediate],
	  {Any: {All: {None: [?Weather.Change.Notified]},
				  {Any: [?Weather.Weather.Now = Drizzle], [?Weather.Weather.Now = LightRain]},
				  {Any: [?Weather.Weather.30Min = Dry], [?Weather.Weather.30Min.Index >= 4]}},
			[?Weather.Change.Notified > ?Weather.Weather.30Min.Index]}} =>
		(Prove: weatherForecastNotification(?Lap, !Weather.Weather.30Min, 30))
{All: [?Lap > 1], [?Tyre.Compound = Wet],
	  {Any: {All: {None: [?Weather.Change.Notified]}, [?Weather.Weather.Now.Index >= 3], [?Weather.Weather.30Min.Index <= 2]},
			[?Weather.Change.Notified > ?Weather.Weather.30Min.Index]}} =>
		(Prove: weatherForecastNotification(?Lap, !Weather.Weather.30Min, 30))
		
updateWeatherTyreCompoundColorTarget(?compound) <= availableTyreCompounds(?compound, ?compoundColor),
												   Set(Weather.Tyre.Compound.Color.Target, ?compoundColor)

priority: -10, [?Pitstop.Lap = ?Lap] => (Prove: clearWeatherTyreChangeNotification(?Lap)),
										(Clear: Weather.Tyre.Compound.Target), (Clear: Weather.Tyre.Compound.Color.Target)

clearWeatherTyreChangeNotification(?lap) <= ?temp = ?lap + 2, ?clearLap = ?temp + !Session.Settings.Lap.Learning.Laps,
											Set(Weather.Tyre.Change.Clear, ?clearLap)

priority: -10, [?Weather.Tyre.Change.Clear = ?Lap] => (Clear: Weather.Tyre.Change.Recommended)

weatherForecastNotification(?lap, ?weather, ?minutes) <= remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000,
														 ?remainingMinutes > ?minutes, notifyWeatherForecast(?weather, ?minutes, true),
														 weatherIndex(?weather, ?index), Set(Weather.Change.Notified, ?index)
weatherForecastNotification(?lap, ?weather, ?minutes) <= remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000,
														 =<(?remainingMinutes, ?minutes), notifyWeatherForecast(?weather, ?minutes, false),
														 weatherIndex(?weather, ?index), Set(Weather.Change.Notified, ?index)

notifyWeatherForecast(?weather, ?minutes, ?change) <= Call(raiseEvent, WeatherForecast, ?weather, ?minutes,  ?change), !
notifyWeatherForecast(?weather, ?minutes, ?change) <= Call(notifyWeatherForecast, ?weather, ?minutes, ?change)

weatherTyreChangeRecommendation(?lap, ?weather, ?minutes, ?compound) <=
		remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000, ?remainingMinutes > ?minutes,
		requestTyreChange(?weather, ?minutes, ?compound),
		updateWeatherNotified(?compound, ?weather), updateTyreChangeRecommended(?lap, ?weather),
		Set(Weather.Tyre.Compound.Target, ?compound), tyreCompoundColor(?compound, ?color), Set(Weather.Tyre.Compound.Color.Target, ?color)
weatherTyreChangeRecommendation(?lap, ?weather, ?minutes, ?compound) <=
		remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000, =<(?remainingMinutes, ?minutes),
		notifyWeatherForecast(?weather, ?minutes, false),
		updateWeatherNotified(?compound, ?weather), updateTyreChangeRecommended(?lap, ?weather)

gripTyreChangeRecommendation(?lap, ?compound) <=
		requestTyreChange(!Weather.Weather.10Min, 0, ?compound),
		updateWeatherNotified(?compound, !Weather.Weather.10Min),
		updateTyreChangeRecommended(?lap, !Weather.Weather.10Min)

requestTyreChange(?weather, ?minutes, ?compound) <= Call(raiseEvent, WeatherForecast, ?weather, ?minutes, true, ?compound), !
requestTyreChange(?weather, ?minutes, ?compound) <= Call(requestTyreChange, ?weather, ?minutes, ?compound)

updateTyreChangeRecommended(?lap, ?weather) <= ?lap > !Session.Settings.Lap.Learning.Laps, !,
											   weatherIndex(?weather, ?index), Set(Weather.Tyre.Change.Recommended, ?index)
updateTyreChangeRecommended(?, ?)

updateWeatherNotified(?, ?weather) <=
		unbound?(!Weather.Change.Notified), !, weatherIndex(?weather, ?index), Set(Weather.Change.Notified, ?index)
updateWeatherNotified(Dry, ?weather) <=
		weatherIndex(?weather, ?index), ?index < !Weather.Change.Notified, !, Set(Weather.Change.Notified, ?index)
updateWeatherNotified(Intermediate, ?weather) <=
		weatherIndex(?weather, ?index), ?index > !Weather.Change.Notified, !, Set(Weather.Change.Notified, ?index)
updateWeatherNotified(Wet, ?weather) <=
		weatherIndex(?weather, ?index), ?index > !Weather.Change.Notified, !, Set(Weather.Change.Notified, ?index)
updateWeatherNotified(?, ?)
